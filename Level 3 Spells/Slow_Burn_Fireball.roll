!scriptcard {{
    --/|**********Customizable Variables***************
    --#overridetemplate|wizard
    --#title|Slow-Burn-Fireball
    --&SpellRadius|4
    --=SpellSaveDC|@{selected|spell_save_dc}
    --&SpellSaveType|Dexterity
    --=BonusDamage|0
    --&BonusDamageType|[FEAT]
    --=DamageDice|?{Spell Level?|3,4d6|4,5d6|5,6d6|6,7d6|7,8d6|8,9d6|9,10d6}
    --&DamageType|fire
    --&DamageFX|beam-fire
    --&DamageFXB|nova-fire
    --&Description|A bright streak flashes from your pointing finger to a point you choose within range, where it slowly ignites the air itself with phlogiston. Each creature in a 20-foot-radius sphere around the target point takes 4d6 fire damage, or half damage with a successful Dexterity saving throw. Every target that takes damage takes the same amount of damage again at the start of your next two turns unless it uses an action to put out the flames on its clothing, hair, and other accoutrements. Flammable objects in the area that arent being worn or carried are also ignited. At Higher LevelsWhen you cast this spell using a spell slot of 4th level or higher, the damage increases by 1d6 for each slot level above 3rd.

    --/|****************DO NOT CHANGE******************
    --#sourceToken|@{selected|token_id}
    --#emoteText|@{selected|token_name}
    --~|array;pagetokens;AllTokens;@{selected|token_id}

    --/|*******************Main************************
    -->RollDamage|
    -->OutputText|
    --%CurrentToken|foreach;AllTokens 
        --&CharName|[*[&CurrentToken]:character_name]
        --?[&CharName(length)] -le 0|%
        -->GetTargetDistance|[&CurrentToken]
        --?[$TargetDistance] -gt [&SpellRadius]|%
        --?"[*[&CurrentToken]:t-statusmarkers]" -ninc advantage 
            -and "[*[&CurrentToken]:t-statusmarkers]" -ninc disadv|>MakeSavingThrow;[&CurrentToken];1d20;Normal
        --?"[*[&CurrentToken]:t-statusmarkers]" -inc disadv|>MakeSavingThrow;[&CurrentToken];2d20kl1;Disadvantage
        --?"[*[&CurrentToken]:t-statusmarkers]" -inc advantage|>MakeSavingThrow;[&CurrentToken];2d20kh1;Advantage
        --?[$TargetSave] -ge [$SpellSaveDC]|>SetSaveBool;Succeeds
        --?[$TargetSave] -lt [$SpellSaveDC]|>SetSaveBool;Fails
        --C[&SaveBool]|Fails:>CaseSaveFails;[&CurrentToken]|Succeeds:>CaseSaveSucceeds;[&CurrentToken]
        -->ReportDamage|
        -->ApplyDamage|[&CurrentToken];1;-[$TotalDamage]
        --*|[hr]
    --%|
    -->PlayEffects|
    -->PlayEffectsB|
    --X|

    --/|****************Subroutines********************
    --:RollDamage|
        --=Damage|[$DamageDice] + [$BonusDamage]
        --=DoubleDamage|[$Damage] * 2
        --=HalfDamage|[$Damage] \ 2
        --=QuarterDamage|[$Damage] \ 4
    --<|

    --:OutputText|
        --+|DC [$SpellSaveDC.Text] [&SpellSaveType] Saving Throw
        --+Damage Roll|[$DamageDice]
        --?[$BonusDamage] -ne 0|>OutputBonusDamage
        --+|[br]
        --+|[rbutton]Show Description::OutputDescription[/rbutton]
    --<|

    --:OutputBonusDamage|
        --=AdditionalDamage|[$BonusDamage] + [&BonusDamageType]
        --+Bonus Damage|[$AdditionalDamage]
    --<|

    --:GetTargetDistance|
        --~TargetDistance|distance;@{target|token_id};[%1%]
        --*[*[%1%]:character_name] Distance|[$TargetDistance]
    --<|

    --:MakeSavingThrow|
        --=TargetSave|[%2%] + [*[%1%]:dexterity_save_bonus]
        --*[*[%1%]:character_name]|[&SpellSaveType] Save [$TargetSave]
        --*Roll|[%3%]
    --<|

    --:SetSaveBool|
        --&SaveBool|[%1%]
    --<|

    --:CaseSaveFails|
        --*|Save Fails!
        -->CalculateDamage|[$Damage];Normal
        --?"[*[%1%]:npc_vulnerabilities]" -inc [&DamageType]|>CalculateDamage;[$DoubleDamage];Vulnerable!
        --?"[*[%1%]:npc_resistances]" -inc [&DamageType]|>CalculateDamage;[$HalfDamage];Resistant!
        --?"[*[%1%]:npc_immunities]" -inc [&DamageType]|>CalculateDamage;0;Immune!
    --<|

    --:CaseSaveSucceeds|
        --*|Save Suceeds!
        -->CalculateDamage|[$HalfDamage];Normal
        --?"[*[%1%]:npc_vulnerabilities]" -inc [&DamageType]|>CalculateDamage;[$Damage];Vulnerable!
        --?"[*[%1%]:npc_resistances]" -inc [&DamageType]|>CalculateDamage;[$QuarterDamage];Resistant!
        --?"[*[%1%]:npc_immunities]" -inc [&DamageType]|>CalculateDamage;0;Immune!
    --<|

    --:CalculateDamage|
        --=TotalDamage|[%1%]
        --&CreatureResist|[%2%]
    --<|

    --:ReportDamage|
        --*Resistance|[&CreatureResist]
        --*|Creature takes [$TotalDamage] [&DamageType] Damage
    --<|

    --:ApplyDamage|
        --@token-mod|_ignore-selected _ids [%1%] _set bar[%2%]_value|[%3%]
    --<|

    --:PlayEffects|
        --vbetweentokens|@{selected|token_id} @{target|token_id} [&DamageFX]
    --<|    

    --:PlayEffectsB|
        --vtoken|@{target|token_id} [&DamageFXB]
    --<|  
    
    --:OutputDescription|
        --+|[f11][&Description][/f]
    --<|  
}}