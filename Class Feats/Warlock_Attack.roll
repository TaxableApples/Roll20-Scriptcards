!scriptcard  {{

  --/|****************Declarations*******************
  --#overridetemplate|wizard
  --#title|Hexblade Rapier
  --=CritRoll|20
  --=AttackModifier|@{selected|charisma_mod}
  --=DamageModifier|@{selected|charisma_mod}
  --&AttackModifierType|[CHA]
  --=ProficiencyBonus|@{selected|pb}
  --=MagicWeaponBonus|0
  --=DamageDice|1d8
  --=CritDice|1d8
  --&DamageType|piercing
  --&FeatDamageType|FEAT
  --=FeatDamage|[$FeatDamage.RollText]
  --=CritFeatDamage|0
  --=DuelBonus|0

  --/|*****Boolean Flags, see functions for damage assignment*****
  --=HexbladeCurse|?{Hexblade's Curse|Yes,1|No,0}
  --=BoomingBlade|?{Booming Blade|Yes,1|No,0}

  --/|****************DO NOT CHANGE******************
  --#sourceToken|@{selected|token_id}
  --#targetToken|@{target|token_id}
  --#emoteText|@{selected|token_name}
  --=TargetAC|@{target|npc_ac}
  --=HexbladeCurseBonus|0
  --=BoomingBladeDamage|0
  --=CritBoomingBladeDamage|0
  --=Resistance|0
  --=Immunity|0
  --=Vulnerability|0
  --=TotalDamage|0
  --=Zero|0
  --=AttackRoll|?{Attack|Standard,1d20|Advantage,2d20kh1|Disadvantage,2d20kl1}

  --/|*******************Main************************
  -->ExecuteBoomingBlade|
  -->ExecuteHexbladeCurse|
  -->MakeAttack|
  -->CalculateDamage|
  -->ApplyDamage|@{target|token_id};1;-[$TotalDamage]
  --X|
  
  --/|****************Subroutines********************
  --:MakeAttack|
    --=Attack|[$AttackRoll] + [$AttackModifier] + [$ProficiencyBonus] + [$DuelBonus] + [$MagicWeaponBonus]
    --+Attack Roll| [$AttackRoll]
    --=AttackBonus|[$AttackModifier] [&AttackModifierType] + [$ProficiencyBonus] [PROF] + [$DuelBonus] [DUEL] + [$MagicWeaponBonus] [MAGIC]
    --+Attack Bonus| [$AttackBonus]
    --+|[f18]Total Attack [$Attack][/f]
    --+|[hr]
    --?[$AttackRoll.Base] -eq [$CritRoll]|Crit
    --?[$Attack.Total] -ge [$TargetAC]|Hit | Miss
  --<|

  --:Crit|
    --=Damage|[$DamageDice] + [$CritDice] + [$DamageModifier] + [$BoomingBladeDamage] + [$HexbladeCurseBonus] + [$FeatDamage] + [$CritFeatDamage] + [$MagicWeaponBonus]
    --+Damage Roll|[$DamageDice] + [$CritDice] [CRIT]
    --=DamageBonuses|[$DamageModifier] [&AttackModifierType] + [$BoomingBladeDamage] [BOOM] + [$CritBoomingBladeDamage] [BMB] + [$HexbladeCurseBonus] [HEX] + [$FeatDamage] [FEAT] + [$CritFeatDamage] [FEAT] + [&MagicWeaponBonus] [MAGIC]
    --+Damage Bonus|[$DamageBonuses]
    --+|[f18][#ff0000]Critical Hit[/#] for [$Damage] [&DamageType] damage![/f]
    -->CheckResistances|
  --<|
  
  --:Hit|
    --=Damage|[$DamageDice] + [$DamageModifier] + [$ProficiencyBonus] + [$FeatDamage] + [&MagicWeaponBonus]
    --+Damage Roll|[$DamageDice]
    --=DamageBonuses|[$DamageModifier] [&AttackModifierType] + [$BoomingBladeDamage] [BOOM] + [$HexbladeCurseBonus] [HEX] + [$FeatDamage] [FEAT] + [&MagicWeaponBonus] [MAGIC]
    --+Damage Bonus|[$DamageBonuses]
    --+|[f18]Attack Hit for [$Damage] [&DamageType] Damage![/f]
    -->CheckResistances|
  --<|

  --:Miss|
    --=Damage|0
    --+|[f18]Attack [#ff0000]missed[/#]![/f]
  --<|

  --:CheckResistances|
    --?"[*T:npc_resistances]" -inc [&DamageType]|ApplyResistance
    --?"[*T:npc_immunities]" -inc [&DamageType]|ApplyImmunity
    --?"[*T:npc_vulnerabilities]" -inc [&DamageType]|ApplyVulnerability
  --<|
    
  --:ApplyResistance|
    --=Resistance|[$Damage] \ 2
    --*|Creature is resistant to [&DamageType] damage!
  --<|
  
  --:ApplyImmunity|
    --=Immunity|[$Damage]
    --*|Creature is immune to [&DamageType] damage!
  --<|
  
  --:ApplyVulnerability|
    --=Vulnerability|[$Damage]
    --+|[hr]
    --*|Creature is vulnerable to [&DamageType] damage!
  --<|
  
  --:CalculateDamage|
    --=TotalDamage|[$Damage]-[$Resistance]-[$Immunity]+[$Vulnerability]
  --<|

  --:ApplyDamage|
    --@token-mod|_ignore-selected _ids [%1%] _set bar[%2%]_value|[%3%]
  --<|

  --:ExecuteBoomingBlade|
    --?[$BoomingBlade] -gt [$Zero] |BoomingBladeMsg
  --<|

  --:BoomingBladeMsg|
    --=BoomingBladeDamage|0
    --=CritBoomingBladeDamage|0
    --+Spell Attack: | Booming Blade
  --<|

  --:ExecuteHexbladeCurse|
    --?[$HexbladeCurse] -gt [$Zero]| HexbladeCurseMsg
  --<|

  --:HexbladeCurseMsg|
    --+Bonus: | Hexblade's Curse
    --=CritRoll|19
    --=HexbladeCurseBonus|[$ProficiencyBonus]
  --<|
}}