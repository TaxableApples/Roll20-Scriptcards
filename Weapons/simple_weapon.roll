!scriptcard  {{

  --/|****************Declarations*******************
    --#overridetemplate|wizard
    --#title|Club
    --=AttackModifier|@{selected|strength_mod}
    --=DamageModifier|@{selected|strength_mod}
    --&AttackModifierType|[STR]
    --=MagicWeaponBonus|0
    --=DamageDice|1d4
    --=CritDice|[$DamageDice.RollText]
    --&DamageType|bludgeoning
    --=DuelBonus|0
    --&FeatDamageType|None
    --=FeatDamage|0
    --=CritFeatDamage|0
    
  --/|****************DO NOT CHANGE******************
  --#sourceToken|@{selected|token_id}
  --#targetToken|@{target|token_id}
  --#emoteText|@{selected|token_name}
  --=TargetAC|@{target|npc_ac}
  --=ProficiencyBonus|@{selected|pb}
  --=CritRoll|20
  --=Resistance|0
  --=Immunity|0
  --=Vulnerability|0
  --=TotalDamage|0
  --=AttackMod|0
  --=DamageMod|0
  --=CritDamageMod|0
  --&NoModText|NoRepeatingAttributeLoaded
  --=AttackRoll|?{Attack|Standard,1d20|Advantage,2d20kh1|Disadvantage,2d20kl1}

  --/|*******************Main************************
  -->MakeAttack|
  -->CalculateDamage|
  -->ApplyDamage|@{target|token_id};1;-[$TotalDamage]
  --X|
  
  --/|****************Subroutines********************
  --:MakeAttack|
    -->CheckForGlobalAttackMod|
    --=Attack|[$AttackRoll] + [$AttackModifier] + [$ProficiencyBonus] + [$AttackMod] + [$DuelBonus] + [$MagicWeaponBonus]
    --+Attack Roll| [$AttackRoll]
    --=AttackBonus|[$AttackModifier] [&AttackModifierType] + [$ProficiencyBonus] [PROF] + [$DuelBonus] [DUEL] + [$MagicWeaponBonus] [MAGIC]
    --+Attack Bonus| [$AttackBonus]
    --+|[f18]Total Attack [$Attack][/f]
    --+|[hr]
    --?[$AttackRoll.Base] -eq [$CritRoll]|Crit
    --?[$Attack.Total] -ge [$TargetAC]|Hit | Miss
  --<|

  --:CheckForGlobalAttackMod|
    --Rfirst|@{selected|character_id};repeating_tohitmod
    --?[*R:global_attack_active_flag] -eq 1|ApplyGlobalAttackMod
  --<|

  --:ApplyGlobalAttackMod|
    --=AttackMod|[*R:global_attack_roll]
    --+Attack Mod:|[*R:global_attack_name] [$AttackMod]
  --<|

  --:CheckForGlobalDamageMod|
    --Rfirst|@{selected|character_id};repeating_damagemod
    --?[$AttackRoll.Base] -eq [$CritRoll] -and [*R:global_damage_active_flag] -eq 1|CritApplyGlobalDamageMod
    --?[$AttackRoll.Base] -ne [$CritRoll] -and [*R:global_damage_active_flag] -eq 1|ApplyGlobalDamageMod
  --<|

  --:CritApplyGlobalDamageMod|
    --=DamageMod|[*R:global_damage_damage]
    --=CritDamageMod|[*R:global_damage_damage]
    --=GlobalCritDmgTxt|[$DamageMod] + [$CritDamageMod] [CRIT]
    --+Damage Mod:|[*R:global_damage_name] [$GlobalCritDmgTxt]
  --<|

  --:ApplyGlobalDamageMod|
    --=DamageMod|[*R:global_damage_damage]
    --+Damage Mod:|[*R:global_damage_name] [$DamageMod]
  --<|

  --:Crit|
    -->CheckForGlobalDamageMod|
    --=Damage|[$DamageDice] + [$CritDice] + [$DamageModifier] + [$FeatDamage] + [$CritFeatDamage] + [$DamageMod] + [$CritDamageMod] + [$MagicWeaponBonus]
    --+Damage Roll|[$DamageDice] + [$CritDice] [CRIT]
    --=DamageBonuses|[$DamageModifier] [&AttackModifierType] + [$FeatDamage] [&FeatDamageType] + [$CritFeatDamage] [&FeatDamageType] + [$DamageMod] [MOD] + [$CritDamageMod] [MOD] + [&MagicWeaponBonus] [MAGIC]
    --+Damage Bonus|[$DamageBonuses]
    --?[$FeatDamage] -ne 0|>ShowCritFeatDamage
    --+|[f18][#ff0000]Critical Hit[/#] for [$Damage] [&DamageType] damage![/f]
    -->CheckResistances|
  --<|
  
  --:Hit|
    -->CheckForGlobalDamageMod|
    --=Damage|[$DamageDice] + [$DamageModifier] + [$FeatDamage] + [$DamageMod] + [&MagicWeaponBonus]
    --+Damage Roll|[$DamageDice]
    --=DamageBonuses|[$DamageModifier] [&AttackModifierType] + [$FeatDamage] [&FeatDamageType] + [$DamageMod] [MOD] + [&MagicWeaponBonus] [MAGIC]
    --+Damage Bonus|[$DamageBonuses]
    --?[$FeatDamage] -ne 0|>ShowFeatDamage
    --+|[f18]Attack Hit for [$Damage] [&DamageType] Damage![/f]
    -->CheckResistances|
  --<|

  --:ShowFeatDamage|
    --+[&FeatDamageType]|[$FeatDamage]
  --<|

  --:ShowCritFeatDamage|
    --+[&FeatDamageType]|[$FeatDamage] + [$CritFeatDamage]
  --<|

  --:Miss|
    --=Damage|0
    --+|[f18]Attack [#ff0000]missed[/#]![/f]
  --<|

  --:CheckResistances|
    --?"[*T:npc_resistances]" -inc [&DamageType]|ApplyResistance
    --?"[*T:npc_immunities]" -inc [&DamageType]|ApplyImmunity
    --?"[*T:npc_vulnerabilities]" -inc [&DamageType]|ApplyVulnerability
  --<|
    
  --:ApplyResistance|
    --=Resistance|[$Damage] \ 2
    --*|Creature is resistant to [&DamageType] damage!
  --<|
  
  --:ApplyImmunity|
    --=Immunity|[$Damage]
    --*|Creature is immune to [&DamageType] damage!
  --<|
  
  --:ApplyVulnerability|
    --=Vulnerability|[$Damage]
    --+|[hr]
    --*|Creature is vulnerable to [&DamageType] damage!
  --<|
  
  --:CalculateDamage|
    --=TotalDamage|[$Damage]-[$Resistance]-[$Immunity]+[$Vulnerability]
  --<|

  --:ApplyDamage|
    --@token-mod|_ignore-selected _ids [%1%] _set bar[%2%]_value|[%3%]
  --<|

}}